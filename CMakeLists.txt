CMAKE_MINIMUM_REQUIRED(VERSION 3.6.2)

PROJECT(poker)

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

SET(UNITY_BUILD OFF)

FUNCTION(enable_unity_build UB_SUFFIX SOURCE_VARIABLE_NAME)
    SET(files ${${SOURCE_VARIABLE_NAME}})
    # Generate a unique filename for the unity build translation unit
    SET(unit_build_file ${CMAKE_CURRENT_BINARY_DIR}/ub_${UB_SUFFIX}.cc)
    # Exclude all translation units from compilation
    SET_SOURCE_FILES_PROPERTIES(${files} PROPERTIES HEADER_FILE_ONLY true)
    # Open the ub file
    FILE(WRITE ${unit_build_file} "// Unity Build generated by CMake\n")
    # Add include statement for each translation unit
    FOREACH(source_file ${files} )
        FILE( APPEND ${unit_build_file} "#include <${CMAKE_CURRENT_SOURCE_DIR}/${source_file}>\n")
    ENDFOREACH(source_file)
    # Complement list of translation units with the name of ub
    SET(${SOURCE_VARIABLE_NAME} ${${SOURCE_VARIABLE_NAME}} ${unit_build_file} PARENT_SCOPE)  
ENDFUNCTION(enable_unity_build)


ADD_SUBDIRECTORY(src)
